plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}
apply plugin: 'maven-publish'

android {
    compileSdk 32
    namespace = 'com.TodayTixGroup.designtokens'
    publishNonDefault true

    defaultConfig {
        minSdk 21
        targetSdk 32
        version "0.1.13"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        aarMetadata {
            minCompileSdk = 21
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "retailer"
    productFlavors {
        todaytix {
            dimension = "retailer"
        }
        goldstar {
            dimension = "retailer"
        }
    }

    publishing {
//        singleVariant("todaytixDebug")
        // TODO: when we do the work for multiple retailers, we need this
        // to have goldstar and today be published under the same library
        multipleVariants("todaytix") {
            includeBuildTypeValues("release")
            includeFlavorDimensionAndValues("retailer", "todaytix")
        }

        multipleVariants("goldstar") {
            includeBuildTypeValues("release")
            includeFlavorDimensionAndValues("retailer", "goldstar")
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            todaytix(MavenPublication) {
                from components.todaytix
                groupId = 'com.TodayTixGroup.designtokens'
                artifactId = 'designTokens-todaytix'
                version = project.version
            }
            goldstar(MavenPublication) {
                from components.goldstar
                groupId = 'com.TodayTixGroup.designtokens'
                artifactId = 'designTokens-goldstar'
                version = project.version
            }
        }
    }
}